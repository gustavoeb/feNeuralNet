/**

This is are aditional methods to the factory Mat structure.

*/

require Math;
require FileIO;
require Containers;

/// \dfgPresetOmit
function Mat_d(Mat mat) {
  this.rows = mat.rows();
  this.cols = mat.cols();
  
  Scalar floatVals[];
  floatVals = mat.getAll();
  this.vals.resize(floatVals.size());
  
  for(Integer i=0; i<floatVals.size(); i++){
    this.vals[i] = floatVals[i];
  }
}

/// A constructor that will read matrices from .txt files. MATlab's dlmwrite() notation is expected.
/// \param file A text file in MATlabs ASCII formatting for matrices.
/// \dfgPresetFolder Mat
function Mat_d Mat_dFromCSV(FilePath file) {
  if(!file.exists())
    throw FUNC + ": file " + file + " does not exist.";
  if(!file.extension())
    throw FUNC + ": file " + file + " extension not supported.";
  TextReader reader = TextReader(file.string());
  String row[] = reader.readLine().split(',');
  reader.seek(0);
  String all[] = reader.readAll().splitAny(',\n');
  Float64 allScalars[];
  for (Integer i = 0; i < all.size()-1; i++) {
      allScalars.push(all[i].toFloat64());
  }
  Integer cols = row.size();
  Integer rows = (all.size()-1)/row.size();
  Mat_d mat(rows,cols);
  mat.setAll(allScalars);

  return mat;
}


/// Gell all the matrix element as a scalar array
/// \dfgPresetOmit
function Float64[] Mat_d.getAll() {
  return this.vals;
}


/// \dfgPresetOmit
function Float64 Mat_d.get(in Integer index) {
  return this.vals[index];
}

/// A value setter for a single value using a single unidimensional index.
/// \param index An unidimensional index describing a position in a matrix counting elements from left to right, top to bottom.
/// \param value A scalar value to be set.
/// \dfgPresetTitle Set_unidimensional
/// \dfgPresetOmit
function Mat_d.set!(Integer index, Float64 value) {
  this.vals[index] = value;
}

/// A value setter for a single value in multiple indices.
/// \param index An array of unidimensional indices describing a position in a matrix counting elements from left to right, top to bottom.
/// \param value A scalar value to be set in all positions of the index array.
/// \dfgPresetTitle SetMany_unidimensional
/// \dfgPresetOmit
function Mat_d.set!(Integer set[], Float64 value) {
  for (Integer i = 0; i < set.size(); i++) {
    this.vals[set[i]] = value;
  }
}

/// Substitutes the matrix value contatiner.
/// \dfgPresetOmit
function Mat_d.setAll!(Float64 vals[]) {
  if(this.vals.size() != vals.size())
    throw FUNC + ": size of array must match the matrix's number of elements (" + this.vals.size() + " vs " + vals.size() + ")";
  this.vals = vals;
}

/// A value setter for a multiple values in multiple indices.
/// \param index An array of unidimensional indices describing a position in a matrix counting elements from left to right, top to bottom.
/// \param value An array of scalar values to be set in all positions of the index array.
/// \dfgPresetTitle SetArray_unidimensional
/// \dfgPresetOmit
function Mat_d.set!(Integer set[], Float64 value[]) {
  if (set.size() != value.size())
    throw FUNC + ": number of indices is different from number of values (" + set.size() + " of " + value.size() + ")";
  for (Integer i = 0; i < set.size(); i++) {
    this.vals[set[i]] = value[i];
  }
}

/// A value getter for a row in a matrix.
/// \param index The index of the row.
function Float64[] Mat_d.getRow(Integer index) {
  Float64 row[];
  if (index > this.rows)
    throw FUNC + ": row index greater than number of rows (" + index + " of " + this.rows + ")";
  if (index < 0)
    throw FUNC + ": row index should be greater than zero (got " + index + ")";
  for (Integer i = 0; i < this.cols; i++) {
    row.push(this.vals[(index)*this.cols+i]);
  }
  return row;
}

/// A value setter for a row in a matrix.
/// \param index The index of the row.
function Mat_d.setRow!(Integer index, Float64 vals[]) {
  if (index > this.rows)
    throw FUNC + ": row index greater than number of rows (" + index + " of " + this.rows + ")";
  if (index < 0)
    throw FUNC + ": row index should be greater than zero (got " + index + ")";
  if (vals.size() != this.cols)
    throw FUNC + ": input values do not match do not match # of elements in row (" + vals.size() + " of " + this.cols + ")";
  for (Integer i = 0; i < this.cols; i++) {
    this.vals[(index)*this.cols+i] = vals[i];
  }
}

/// A value getter for a column in a matrix.
/// \param index The index of the column.
function Float64[] Mat_d.getColumn(Integer index) {
  Float64 column[];
  if (index > this.cols)
    throw FUNC + ": colum index greater than number of columns (" + index + " of " + this.cols + ")";
  if (index < 0)
    throw FUNC + ": column index should be greater than zero (got " + index + ")";
  for (Integer i = 0; i <= this.rows - 1; i++) {
    column.push(this.vals[(i)*this.cols+index]);
  }
  return column;
}

/// A value setter for a column in a matrix.
/// \param index The index of the column.
function Mat_d.setColumn!(Integer index, Float64 vals[]) {
  Float64 column[];
  if (index > this.cols)
    throw FUNC + ": colum index greater than number of columns (" + index + " of " + this.cols + ")";
  if (index < 0)
    throw FUNC + ": column index should be greater than zero (got " + index + ")";
  if (vals.size() != this.rows)
    throw FUNC + ": input values do not match do not match # of elements in column (" + vals.size() + " of " + this.rows + ")";
  for (Integer i = 0; i <= this.rows - 1; i++) {
    this.vals[(i)*this.cols+index] = vals[i];
  }
}

/// Concatenates two matrices vertically.
/// \param mat A matrix to be appended after the last row
/// \dfgPresetOmit
function Mat_d.vertcat!(Mat_d mat) {
  if (this.cols != mat.cols)
    throw FUNC + ": incompatible dimensions (" + this.sizeDesc() + " vs. " + mat.sizeDesc() + ")";
  this.rows = this.rows + mat.rows;
  this.vals = this.vals + mat.vals;
}

/// Concatenates two matrices horizontally.
/// \param mat A matrix to be appended after the last column.
/// \dfgPresetOmit
function Mat_d.horzcat!(in Mat_d mat) {
  // This is a sloppy implementation.
  // Should refactor when possible.
  Mat_d thisClone = this.transpose();
  Mat_d matClone = mat.transpose();
  thisClone.vertcat(matClone);
  this = thisClone.transpose();
}


/// Multiplies a matrix by a column vector.
/// \param vector Array of scalar values for the multiplication.
function Mat_d Mat_d.multiplyByColVector(Float64 vector[]) {
  Mat_d columnVec(1,vector.size());
  columnVec.setAll(vector);
  return this * columnVec;
}

/// Multiplies a matrix by a row vector.
/// \param vector Array of scalar values for the multiplication.
function Mat_d Mat_d.multiplyByRowVector(Float64 vector[]) {
  Mat_d rowVec(vector.size(),1);
  rowVec.setAll(vector);
  return this * rowVec;
}

/// Multiplies a matrix by a column vector.
/// \param vector Array of scalar values for the multiplication.
function Mat_d Mat_d.multiplyColVectorByMat(Float64 vector[]) {
  Mat_d columnVec(1,vector.size());
  columnVec.setAll(vector);
  return columnVec * this;
}

/// Multiplies a matrix by a row vector.
/// \param vector Array of scalar values for the multiplication.
function Mat_d Mat_d.multiplyRowVectorByMat(Float64 vector[]) {
  Mat_d rowVec(vector.size(),1);
  rowVec.setAll(vector);
  return rowVec * this;
}

/// A value getter for a sequence of columns in a matrix.
/// \param start The first column in the sequence.
/// \param end The last column in the sequence.
function Mat_d Mat_d.getColumns(Integer start, Integer end) {
  Float64 newVals[];
  if (start > this.cols || end > this.cols)
    throw FUNC + ": colum index greater than number of columns (" + start + ", " + end + " of " + this.rows + ")";
  if (start < 0 || end < 0)
    throw FUNC + ": column index should be greater than zero";
  if (end < start)
    throw FUNC + ": start is greater than end (" + start + " vs " + end + ")";
  for (Integer j = start; j <= end; j++) {
    for (Integer i = 0; i <= this.rows - 1; i++) {
      newVals.push(this.vals[(i)*this.cols+j]);
    }
  }
  Mat_d newMat(end-start+1,this.rows);
  newMat.setAll(newVals);
  return newMat.transpose();
}

/// A value getter for a sequence of rows in a matrix.
/// \param start The first row in the sequence.
/// \param end The last row in the sequence.
function Mat_d Mat_d.getRows(Integer start, Integer end) {
  Mat_d emptuMat(0,0);
  Float64 newVals[];
  if (start > this.rows || end > this.rows)
    throw FUNC + ": row index greater than number of rows (" + start + ", " + end + " of " + this.rows + ")";
  if (start < 0 || end < 0) 
    throw FUNC + ": row index should be greater than zero";
  if (end < start)
    throw FUNC + ": start is greater than end (" + start + " vs " + end + ")";
  for (Integer j = start; j <= end; j++) {
    for (Integer i = 0; i < this.cols; i++) {
      newVals.push(this.vals[(j)*this.cols+i]);
    }
  }
  Mat_d new_mat(end-start+1,this.cols);
  new_mat.setAll(newVals);
  return new_mat;
}